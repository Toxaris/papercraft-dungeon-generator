load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//frontend:eslint/package_json.bzl", eslint_bin = "bin")
load("@jest//:defs.bzl", "jest_test")

ASSET_PATTERNS = [
    "*.svg",
    "*.css",
]

SRC_PATTERNS = [
    "*.tsx",
    "*.ts",
]

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
TEST_PATTERNS = [
    "*.test.tsx",
    "*.test.ts",
    "*.spec.tsx",
    "*.spec.ts",
]

js_library(
    name = "src",
    srcs = glob(ASSET_PATTERNS),
    visibility = ["//visibility:public"],
    deps = [
        ":src_ts",
    ],
)

ts_project(
    name = "src_ts",
    srcs = glob(
        include = SRC_PATTERNS,
        exclude = TEST_PATTERNS,
    ),
    declaration = True,
    resolve_json_module = True,
    tsconfig = "//frontend:tsconfig",
    deps = [
        "//frontend:node_modules/react",
        "//frontend:node_modules/react-dom",
        "//frontend:node_modules/web-vitals",
    ],
)

ts_project(
    name = "src_test_ts",
    srcs = glob(TEST_PATTERNS),
    declaration = True,
    resolve_json_module = True,
    tsconfig = "//frontend:tsconfig",
    deps = [
        ":src_ts",
        "//frontend:node_modules/@testing-library/jest-dom",
        "//frontend:node_modules/@testing-library/react",
        "//frontend:node_modules/@types/jest",
    ],
)

jest_test(
    name = "src_test",
    config = "//frontend:jest_config",
    data = [
        ":src",
        ":src_test_ts",
        "//frontend:node_modules/jest-environment-jsdom",
        "//frontend:node_modules/jest-transform-stub",
    ],
)

eslint_bin.eslint_test(
    name = "eslint_test",
    args = ["{}/{}".format(
        package_name(),
        p,
    ) for p in SRC_PATTERNS],
    data = [
        "//frontend:package_json",
        "//frontend:node_modules/eslint-config-react-app",
    ] + glob(SRC_PATTERNS),
)
